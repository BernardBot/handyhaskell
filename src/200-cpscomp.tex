% !TEX root = document.tex

\chapter{\label{chap:cpscomp}Compiling with Continuations}

In this chapter we will develop the first version of the LamToWat compiler that translates the \icode{Lam} language into the \icode{Wat} language. Our compiler will be written in Haskell \autocite{haskellhomepage} and as such \icode{Lam} and \icode{Wat} are implemented as Haskell data types. We follow a minimal version of the approach by \citeauthor{DBLP:books/daglib/0022396} \autocite{DBLP:books/daglib/0022396}. Consequently, a simplified version of the CPS language used by Appel will serve as IR and have its own data type \icode{Cps}. 

What makes CPS favorable as an IR is that it makes control flow and data flow explicit. These features nicely represent the objective of a compiler: translating from a high-level to a low-level language by describing abstractions in finer detail. CPS uses special functions, called 'continuations', to describe more complex control flow constructs. The fact that continuations are special functions becomes essential when translating languages with first-class functions.

The purpose of building LamToWat is to examine and then improve its IR. The complexity of CPS conversion and difficulties with the untyped IR transformations will become apparent. If we can improve upon the already favorable features of CPS, we are creating a better IR to program in. Although we are not concerned with the peripherals of the compiler, an IR is not used in a vacuum. We want to examine the compiler passes that map to and from the IR as well as the ones that map to the IR itself.

LamToWat is a multi-pass compiler. This means that multiple passes over the IR will be made, each transforming a part of the representation. The complete compiler comprises these IR transformations with additional passes to parse lambda calculus source files and emit WebAssembly, such that it can be compiled and interpreted by the WebAssembly Binary Toolkit () \autocite{wabt}.

Each compiler pass wilt have a type. A type indicates what a compiler pass does. For example, when we transform \icode{Lam} into \icode{Cps} with \icode{2cps}, the type of this  is \icode{lam2cps :: Lam -> Cps}. The double colon indicates a type declaration in Haskell. Some transformations need extra functionality, like generating fresh variable names. This functionality or effect will be handled by a helper function. For the example this will be \icode{l2c}, which can generate fresh variables, but has a much more complicated type. We will focus on the type of the 'main' function, such as \icode{lam2cps} when comparing the two version of LamToWat.

In this version of LamToWat the data types we will be using are \icode{Lam}, \icode{Cps}, and \icode{Wat}. To read in source files we also need the data type \icode{String}. LamToWat will be composed of the sequences of transformations in figure \ref{fig:lam2watv1org}.

\begin{figure}
\begin{gather*}
  String \xto{parse} Lam \xto{lam2cps} Cps \xto{cps2cps} Cps \xto{cps2wat} Wat \xto{emit} String
\end{gather*}
\caption{LamToWat Version 1: compiler organization.}
\label{fig:lam2watv1org}
\end{figure}

LamToWat is split into a front-end and a back-end. The front-end of the compiler is made up of parsing (\icode{parse}), and CPS conversion (\icode{lam2cps}). We will not discuss  parsing in detail, as the topic is orthogonal to the research in this thesis. We use \icode{parsec} \autocite{parsec} to build a parser for our compiler. The back-end of LamToWat consist of three transformations on the IR: closure conversion, hoisting, and emitting (\icode{cps2}). In the first version of the compiler hoisting and closure conversion are combined in \icode{cps2cps}.

The following sections will first describe the data types \icode{Lam}, \icode{Cps}, and \icode{Wat} used in LamToWat with specific attention to \icode{Cps}. We will adhere to the organization of the compiler and discuss the relevant compiler passes over these data types in the order described in figure \ref{fig:lam2watv1org}.

\section{\label{section:datatypes}Data Types}
The data types of LamToWat will be implemented as Haskell data types. Haskell data types are indicated with the \icode{data} keyword. Every data type has a name, which comes after the keyword. A number of constructors follow separated by vertical bars. Data types can be recursively defined. Type aliases indicated with the \icode{type} keyword are also used; providing new names for existing data types.

First, we give definitions of types and data types that are used by all passes of the compiler. We define \icode{Var}iables that indicate names of functions and values with simple \icode{String}s. This is a design choice as it allows for nonsense programs that use undefined variables, but also for easy manipulation of variables and generation of variable names. A \icode{Fun}ction is a triple of a function name, list of function parameter names, and a body. A \icode{Fix}point of functions is a list of functions and a final expression.

\begin{lstlisting}[language=Haskell]
type Var = String
type Fun e = (Var,[Var],e)
type Fix e = ([Fun e],e)
\end{lstlisting}

Our compiler uses a notion of \icode{Val}ues that is incorporated in all data types. We use \icode{LABEL}s for variables indicating function names and \icode{VAR}s for everything else.

\begin{lstlisting}[language=Haskell]
data Val
  = INT Int
  | VAR Var
  | LABEL Var
\end{lstlisting}

\subsection{\label{subsection:expdata}The Lam Language}
The \icode{Lam} data type represents the abstract syntax tree (AST) of the lambda calculus. The three constructors for \icode{Lam}bda abstraction and \icode{App}lication encompass the standard definition of the lambda calculus. \icode{Val}ues are represented by the \icode{Val} constructor. A constructors for \icode{Add}ition is added to make basic arithmetic possible. The lambda calculus is one of the smallest functional languages that still has many of the interesting properties of functional languages, such as first-class functions and recursion. Moreover, the lambda calculus is well-studied \autocite{barendregt1984lambda}. Programs written in the lambda calculus will be used for testing LamToWat.

\begin{lstlisting}[language=Haskell]
data Lam
  = Lam Var Lam
  | App Lam Lam
  | Add Lam Lam
  | Val Val
\end{lstlisting}

Two programs will be used as running examples throughout this thesis to show how the compilation process works. Their representation in different stages of both versions of the compiler can be found in appendix \ref{apdx:a}. In the \icode{Lam} language they are written as follows.

\begin{lstlisting}[language=Haskell]
App (Lam "x" (Add (Val (VAR "x")) (Val (INT 1)))) (Val (INT 41))
\end{lstlisting}

\begin{lstlisting}[language=Haskell]
App
(App
   (Lam "x" (Lam "y" (Add (Val (VAR "x")) (Val (VAR "y")))))
   (Val (INT 13)))
(Val (INT 29))
\end{lstlisting}

The first program applies an anonymous function that adds 1 to its argument \icode{x} to the number 41. The second programs applies a nested anonymous function that adds its arguments to the numbers 13 and 29. Both programs evaluate to the number 42. We choose a program with nested functions and a program without nested functions as examples to display the compilation of this language feature separately.

LamToWat provides a parser for the \icode{Lam} language. The accepted syntax is the same as the lambda calculus syntax in Haskell. The symbols \icode{\} and \icode{->} are used for lambda abstraction and a space is used for application. With this syntax the two example programs are written as follows:

\begin{lstlisting}[language=Haskell]
(\ x -> x + 1) 41
\end{lstlisting}

\begin{lstlisting}[language=Haskell]
(\ x -> (\ y -> x + y)) 13 29
\end{lstlisting}

\subsection{\label{subsection:cpsdata}The Cps Language}
We follow the CPS language data type definition and semantics by \citeauthor{DBLP:books/daglib/0022396} \autocite{DBLP:books/daglib/0022396} with some minor adjustments. It is used as IR because it facilitates compiler transformations relevant to functional programming languages. The data type is similar to the control flow graph of a program. Control flow is modeled with functions and function calls. Data flow is modeled with records. LamToWat uses \icode{Cps} to transform nested, higher-order functions to simple functions.

\icode{Cps} functions are made up of a name, argument names, and a body. They are defined in mutually recursive \icode{Fix}ed function blocks. All expressions except \icode{App} and \icode{Val} have a \icode{Cps} continuation as their last argument. Since the \icode{Add}, \icode{Record}, and \icode{Select} expressions produce a value, they name it before continuing. \icode{Val} represents a final value or return statement, it does not adhere to continuation-passing style. No problems are caused by it, because we will make sure that it is the final expression of our programs. This makes it a global return of the program itself, not of a function.

\begin{lstlisting}[language=Haskell]
data Cps
  = App Val [Val]
  | Val Val
  | Record [Val] Var Cps
  | Select Int Val Var Cps
  | Add Val Val Var Cps
  | Fix [Fun Cps] Cps
\end{lstlisting}

The \icode{Cps} data type guarantees that compiler transformations can be easily implemented by enforcing the following properties:

\begin{itemize}
\item Functions do not return, instead the last thing a function does is call a continuation.
\item Function parameters can only be values.
\item All intermediate values have names.
\end{itemize}

CPS also requires all user functions to have an extra continuation argument. This requirement is not enforced by the data type, but needs to be fulfilled in order for it to be proper CPS. To illustrate how the \icode{Cps} data type is used we will translate the first lambda calculus example into \icode{Cps}:

\begin{lstlisting}[language=Haskell]
Fix [("f", ["x"],
  Add (VAR "x") (INT 1) "r" (Val (VAR "r")))]
(App (VAR "f") [INT 41])
\end{lstlisting}

We define a function in a \icode{Fix} block with name \icode{f} and argument \icode{x}. The body of this function adds \icode{x} and 1, names the result \icode{r}, and returns this result. Finally, we apply \icode{f} to 41.

\subsection{\label{subsection:webdata}The Wat Language}
The \icode{Wat} language is a hierarchical, high-level assembly language and a simplification of WebAssembly \autocite{webassemblyhomepage}. As the final language in the compilation process it gives some guarantees about the well-formedness of the output of the compiler. The fundamental unit of code is a \icode{Fix} made up of \icode{Exp}.

We have chosen \icode{Wat} as the output language of LamToWat, because it is a relatively high-level language without first-class functions. It can be easily translated to WebAssembly, but still has enough abstraction to be able to target other languages. The absence of first-class functions requires LamToWat to actually compile the \icode{Lam} language, which does have this feature. By keeping the output of LamToWat as a Haskell data type, we can compare the semantics of the \icode{Lam}, \icode{Cps}, and \icode{Wat} data types more easily.

If we look at the data type definition below, we see that \icode{Wat} is quite similar to \icode{Cps}. It is easy to translate from \icode{Cps} to \icode{Wat} after closure conversion and hosting. The \icode{Record} and \icode{Select} constructors are replaced with \icode{Malloc}, \icode{Store}, and \icode{Load}. The \icode{LABEL} constructor from \icode{Val} is not used, because functions are now represented by integers serving as an index into the module's function list. \icode{Fix} is replaced with a top-level \icode{Fix}, ensuring that there are no nested functions anymore.

\begin{lstlisting}[language=Haskell]
type Wat = Fix Exp

type Offset = Int
data Exp
  = Malloc Int Var Exp
  | Store Offset Val Val Exp
  | Load Offset Val Var Exp
  | Add Val Val Var Exp
  | App Val [Val]
  | Val Val
\end{lstlisting}

The translation of our simple example program \icode{(\ x -> x + 1) 41} into the \icode{Wat} language is shown below. The only real difference with \icode{Cps} is the use of the an \icode{INT} instead of a \icode{LABEL} to indicate a function pointer.

\begin{lstlisting}[language=Haskell]
([("f",["x"],
  Add (VAR "x") (INT 1) "r" (Val (VAR "r")))],
(App (INT 0) [INT 41]))
\end{lstlisting}

We can emit \icode{Wat} as WebAssembly that can be compiled and interpreted by WABT. When we translate to WebAssembly our program becomes a lot longer and there are many new keywords. WebAssembly requires the programmer to declare some extra information, which leads to a lot of syntactic noise. This information can be deduced from the previous simpler \icode{Wat} expression, so we can emit it automatically, see the following:

\begin{lstlisting}
(module
(memory 1)
(global $_p (mut i32) (i32.const 0))
(table 1 funcref)
(elem (i32.const 0) $f)
(type $_t1 (func (param i32) (result i32)))
(export "_start" (func $_start))
(func $_start  (result i32)
  (call_indirect (type $_t1) (i32.const 41) (i32.const 0))
)
(func $f (param $x i32) (result i32) (local $r i32)
  (local.set $r (i32.add (local.get $x) (i32.const 1)))
  (local.get $r)
)
)
\end{lstlisting}

The first seven lines of the printed WebAssembly program declare that there is one page of mutable global memory and a mutable heap pointer. We declare one function reference of the function \icode{f}. This function has type \icode{_t1}. In order to run the program we need to export the \icode{_start} function. Following the preamble we see the entry point of our program in the form of the function \icode{_start} and our original function \icode{f}. Function signatures now include a result type (which is always \icode{i32} for the programs we compile) and all locally used variables indicated with the \icode{local} keyword. When we call a function we indicate the type of that function with the \icode{type} keyword. To refer to a function we use its index in the function \icode{table}. In this case we refer to the function \icode{f} by \icode{i32.const 0}.

\section{\label{section:transforms}Transformations}
Transformations or compiler passes of LamToWat will be implemented as Haskell functions. These functions will transform the data types of the previous section. A transformation can convert a data type to a different data type, or to the same data type with certain properties. A graph representation of the example lambda programs throughout the compilation process can be found in Appendix \ref{section:v1printsimple} and \ref{section:v1printnest}.

\subsection{\label{subsection:cpsconvert}CPS Conversion}
CPS conversion will transform the \icode{Lam} data type into the \icode{Cps} data type and is the second transformation of LamToWat, see figure \ref{fig:lam2watv1org}. By converting our program to continuation-passing style we make control flow and data flow explicit. In practice this means that we generate fresh variable names for intermediate values and use a meta-continuation (a continuation in the  Haskell) to indicate the order of expressions.

\citeauthor{DBLP:books/daglib/0022396} describes a CPS conversion function that takes an extra meta-continuation as argument. It also assumes some way of generating fresh variable names, which is not further specified. In Haskell the signature of such a  would look as follows. It uses \icode{State} for fresh variable name generation.

\begin{lstlisting}[language=Haskell]
l2c :: Lam -> (Val -> State Int Cps) -> State Int Cps
\end{lstlisting}

\icode{l2c} is split up into cases for each constructor of \icode{Lam}. Converting \icode{Val} is trivial: apply the meta-continuation \icode{c} to it. To convert the anonymous function \icode{Lam} we generate fresh variables \icode{f} and \icode{k} to name the function and its continuation. We pass the function name to the  \icode{c}. We convert the body of the function \icode{e}, giving it a meta-continuation which calls the continuation with the result \icode{z}. Finally, we construct a \icode{Fix} to contain our converted function and update continuation \icode{c'}. The \icode{App} and \icode{Add} cases follow a similar approach.

\begin{lstlisting}[language=Haskell]
l2c (Val v) c = c v
l2c (Lam x e) c = do
  f <- fresh "f"
  k <- fresh "k"
  c' <- c (LABEL f)
   <- l2c e $ \ z -> return $ App (VAR k) [z]
  return $ Fix [(f,[x,k],cf)] c'
l2c (App e1 e2) c = do
  r <- fresh "r"
  x <- fresh "x"
  c' <- c (VAR x)
  cf <- l2c e1 $ \ v1 -> l2c e2 $ \ v2 -> return $ App v1 [v2, LABEL r]
  return $ Fix [(r,[x],c')] cf
l2c (Add e1 e2) c = do
  x <- fresh "x"
  c' <- c (VAR x)
  l2c e1 $ \ v1 -> l2c e2 $ \ v2 -> return $ Add v1 v2 x c'
\end{lstlisting}

How the \icode{l2c} function works is not obvious. We have to generate variable names, pass these to the continuation and sometimes wrap the resulting values in a \icode{Fix}. This is quite complex, control flow should be easier to describe. For binary operators like \icode{App} and \icode{Add} we want to evaluate the left argument first (arbitrarily chosen order), the right argument second and finally add the two resulting values. We would like to write something like the following instead of generating a variable name and exposing the meta-continuation.

\begin{lstlisting}[language=Haskell]
l2c' (Add e1 e2) = do
  v1 <- l2c' e1
  v2 <- l2c' e2
  add v1 v2
\end{lstlisting}

The case for \icode{App} is especially complicated as we have to create a return point function to serve as continuation argument to the final application. Keeping track of continuations becomes even more non-trivial when we have multiple of them. For example when we want to have exception handlers in our source language.

\subsection{\label{section:closconvert}Closure Conversion}
Closure conversion transforms the \icode{Cps} data type into the \icode{Cps} data type where functions do not contain free variables. It is the third transformation in LamToWat, see figure \ref{fig:lam2watv1org}. The free variables of a function are passed to the function via an extra argument. We will package a function with its free variables, this package is called a 'closure'.

There are many approaches to generating closures. We take an approach optimized for simplicity. We implement closures as records, where the first element is a function pointer (implemented as a \icode{LABEL}) and the second element is the environment of free variables. We use effects to keep track of variables (\icode{ask},\icode{local}) in scope and to hoist functions to the top-level (\icode{tell}), which makes the type of \icode{c2c} the following:

\begin{lstlisting}[language=Haskell]
c2c :: Cps ->  [Fun Cps] (Reader [Var]) Cps
\end{lstlisting}

The cases for \icode{Fix} and \icode{App} are at the heart of the transformation. For the other constructors we simply update the environment with their assigned names.

When we encounter a \icode{Fix} we ask what variables are in scope. For each function we add an extra first argument \icode{_closure}: the closure record. We prefix the function body with selecting and naming all variables in scope that do not have the same name as any of the function's arguments. We open the closure by selecting the environment from the closure record and naming it \icode{_env}.

When we encounter an \icode{App} we create a record with all variables in scope and call it \icode{_env}. We pack it with all function pointers to create closures and rename them by prefixing an underscore. There are now two cases for application: closures and function pointers. To apply a function pointer we create a closure and add it as the first argument. To apply a closure we select the closure's first element and apply that to the closure and the closure's original arguments.

\begin{lstlisting}[language=Haskell]
c2c (Fix fs e) = do
  fs' <- mapM funClos fs
  tell fs'
  c2c e
  where funClos (name,args,body) = do
          nv <- ask
          body' <- local (++args) (c2c body)
          return $
            ( name
            , "_closure" : args
            , Select 1 (VAR "_closure") "_env" $
              foldr (openClos args) body' (zip [0..] nv)
            )

        openClos args (i,x) =
          if x `elem` args then id else Select i (VAR "_env") x

c2c (App fun args) = do
  nv <- ask
  return $
    Record (map VAR nv) "_env" $
    foldr mkClos appClos args
  where appClos = case fun of
          LABEL fp -> let cl = '_' : fp in
                        Record [LABEL fp,VAR "_env"] cl $
                        App (LABEL fp) (VAR cl : args')

          VAR cl   -> let fp = '_' : cl in
                        Select 0 (VAR cl) fp $
                        App (VAR fp) (VAR cl : args')

        mkClos (LABEL x) = Record [LABEL x, VAR "_env"] ('_' : x)
        mkClos _ = id

        args' = map rename args

        rename (LABEL x) = VAR $ '_' : x
        rename v = v

c2c (Record vs x e) = withvar x e $ Record vs
c2c (Select i v x e) = withvar x e $ Select i v
c2c (Add v1 v2 x e) = withvar x e $ Add v1 v2
c2c (Val v) = return $ Val v

withvar x e op = do
  e' <- local (++[x]) (c2c e)
  return $ op x e'
\end{lstlisting}

The main defect of \icode{cps2cps} is that it gives no indication of the fact that our \icode{Cps} expressions no longer contain \icode{Fix} expressions. When we encounter a \icode{Fix}, we use the \icode{tell} function to write all functions to a list. It is obvious from the implementation that they are no longer present, however, the type \icode{cps2cps :: Cps -> Cps} of the transformation gives no recognition of this fact. If we were to pass a \icode{Cps} expression to the next pass of the compiler that has nested \icode{Fix}es, it would cause an error.

\subsection{\label{section:emit}Emitting}
Emitting transforms the \icode{Cps} data type into the \icode{Wat} data type. We create a list of function names \icode{ns} to map function names to indices. The most interesting parts of the transformation are for the \icode{Record} case and the \icode{LABEL} case. \icode{Record}s are transformed into a combination of \icode{Malloc} and \icode{Store}, because \icode{Wat} does not support such a high level abstraction as records, but just has simple heap operations. \icode{LABEL}s are mapped to their index in the list of function names, transforming function labels into function pointers.

This transformation could benefit from the reuse of constructors. We see that \icode{App} is transformed to its \icode{Wat} counterpart \icode{App}. We could just leave these parts of the data type untouched and only show the essence of the transformation, it would benefit both the reader and the writer of this code. What the transformation does would be more explicit and less code naturally leads to a smaller number of errors.

\begin{lstlisting}[language=Haskell]
cps2wat :: Cps -> Wat
cps2wat (C.Fix fs e) = (map (fmap c2w) fs,c2w e)
    where ns = map (\ (f,as,b) -> f) fs

          c2w (C.Val v)          = W.Val (v2v v)
          c2w (C.App v vs)       = W.App (v2v v) (map v2v vs)
          c2w (C.Add v1 v2 x e)  = W.Add (v2v v1) (v2v v2) x (c2w e)
          c2w (C.Select i v x e) = W.Load i (v2v v) x (c2w e)
          c2w (C.Record vs x e)  =
            W.Malloc (length vs) x $
            foldr (\ (i,v) -> W.Store i (VAR x) (v2v v))
              (c2w e) (zip [0..] vs)

          v2v (LABEL x) = INT $ fromJust $ x `elemIndex` ns
          v2v v         = v
\end{lstlisting}

In order to run our converted expression we need WABT and indicate where the \icode{wat2wasm} and \icode{wasm-interp} binaries are located. We can then run the command from within a interactive Haskell session with \icode{shake} \autocite{shake}. A \icode{ghci} session where we would run a converted lambda calculus expression would look as follows:

\begin{lstlisting}[language=Haskell]
> putStrLn $ emit $ cps2wat $ cps2cps $ lam2cps $ parse "(\\ x -> x + 1) 41"
(module
...

> Wat.emitRun $ cps2wat $ cps2cps $ lam2cps $ parse "(\\ x -> x + 1) 41"
_start() => i32:42
\end{lstlisting}

\section{\label{section:summarycps}Discussion}
In this chapter we have shown how to build a compiler that translates \icode{Lam} into \icode{Wat}. We used three data types: \icode{Lam}, \icode{Cps}, and \icode{Wat}. By applying the transformations \icode{parse}, \icode{lam2cps}, \icode{cps2cps}, and \icode{cps2wat} to a well formatted lambda calculus string, we obtain a low-level language output that can be printed as WebAssembly. By looking at two example lambda expressions, we tracked the state of our program throughout the compilation process, see Appendix \ref{section:v1printsimple} and \ref{section:v1printnest}.

We identified three problems with the transformations on the data types and the data types itself. These are summarized as follows:

\begin{itemize}
\item Complex specification of control flow in \icode{lam2cps}\\\\
\icode{lam2cps} is itself written in Continuation-Passing Style. It requires us to expose the meta-continuation. This leads to a confusing specification of control flow where the programmer needs to constantly switch between continuation and .
\item No types to indicate change of \icode{Cps} after \icode{cps2cps}\\\\
Closure conversion has type \icode{cps2cps :: Cps -> Cps}. The \icode{Cps} data type is free to contain nested \icode{Fix} expressions. We want to guarantee that functions are no longer nested. All function definitions should be contained in a single, top-level \icode{Fix}. The bodies of the \icode{Fix} should be made up of expressions containing only addition, records, and application.

Alternatively, we could write separate data types for each transformation. These could indicate the types we want. However, this would lead to a lot of duplicate code, as mentioned in the next problem. The number of the extra lines of source code is calculated as follows: multiply the number of lines of your original data type (in our case \icode{Cps}) with the number of transformations you want to perform. We see immediately that this leads to a lot of lines of code for a larger number of transformations.
\item Duplicate constructors in \icode{Cps} and \icode{Wat}\\\\
If we look at both data types we see that addition and application constructors match one-to-one. We would like to only transform \icode{Record} expressions into \icode{Malloc} expressions.
\end{itemize}

In the next chapter we will try to alleviate these shortcomings of the first version of our compiler by proposing a new data type: command trees.
