% !TEX root = document.tex

\chapter{\label{chap:treecomp}Compiling with Command Trees}
In this chapter we propose command trees as an improvement upon the \icode{Cps} data type of the previous chapter. The new compiler uses the \icode{Tree} and \icode{Tps} data type as IR. These data types are modular in the commands they use and have an internalized notion of control flow. This allows us to solve the problems from section \ref{section:summarycps}: complex control flow specification, absence of indicative type changes, and duplicate constructors.

Compiler passes on the new IR will have a type that looks like:

\begin{lstlisting}[language=Haskell]
pass :: Tps cmd Val -> Tps cmd' Val
\end{lstlisting}

The differences between \icode{cmd} and \icode{cmd'} indicate what a pass does.

\begin{figure}
\begin{gather*}
  String \xto{parse}\\
  Lam \xto{lam2tree}\\
  Tree  (Comp + Fix + Base) Val \xto{tree2tps}\\
  Tps          (Fix + Base) Val \xto{hClos}\\
  Tps (Fix + Record + Base) Val \xto{hRecord}\\
  Tps (Fix + Malloc + Base) Val \xto{hFix}\\
  (Fix (Tps (Malloc + Base) Val)) \xto{tps2wat}\\
  Wat \xto{emit}\\
  String
\end{gather*}
\caption{LamToWat Version 2: compiler organization. Transformations are sequenced up to reordering of commands, see \ref{subsection:openunion} and \ref{section:ctreebetter}.}
\label{fig:lam2watv2org}
\end{figure}

If we look at the transformations in figure \ref{fig:lam2watv2org}, we notice that our compiler consist of more transformations than before. We also note the reuse of the \icode{Lam} and \icode{Wat} data types, which are the same as in the previous version of LamToWat. Our notion of \icode{Val}ues remains the same too. We combine different commands by using open unions ($+$ in the figure, and \icode{:+:} in code). The increase in compiler steps does mean an increase in complexity, but also an increase in explicitness and declarativity. The complexity was present in the previous version of LamToWat, but remained hidden. The new type of \icode{Tps} gives the programmer information about the transformations. 

\icode{Tree} is used in the front-end of the compiler, while \icode{Tps} is used in the back-end. This chapter is structured in the same order as the previous chapter: we will first discuss the new \icode{Tree} and \icode{Tps} data types and then examine their transformations. A graph representation of the example lambda programs throughout the compilation process can be found in Appendix \ref{section:v2printsimple} and \ref{section:v2printnest}. Finally, we will suggest command tree improvements and discuss how command trees have solved the problems of the first version of LamToWat.

\section{\label{section:commandtree}Command Trees}
Command trees are a data type used to sequence commands. A well known data type that is also capable of sequencing is a list. Command trees improve upon lists by adding subcontinuations and by providing the ability to use the result of our command in the commands after that. Command trees can be easily sequenced using Haskell's \icode{do}-notation.

The meaning of a command is something that is left to the programmer \autocite{DBLP:journals/corr/PlotkinP13, DBLP:conf/haskell/WuSH14}. What a command does is implemented later in a function called a handler. Command trees are used to model effects in denotational semantics. We can write handlers for effects to create an interpreter. In this chapter we try to extend this approach to writing a compiler. Commands can be used in a modular manner by using open unions. We give two derivations of command trees in section \ref{section:freemonad} and section \ref{section:opmonad} and discuss the role of command trees in (modular) denotational semantics in more detail in section \ref{section:denotationalsemantics}.

The structure and monadic nature of command trees have already been discussed in \citetitle{commandtreespoulsen} \autocite{commandtreespoulsen}. A modified version of the discussion will be restated in this chapter for completeness. A command tree consists of two constructors: \icode{Leaf} and \icode{Node}. \icode{Leaf a} is the smallest form of command tree that exists and simply returns the value \icode{a}. A \icode{Node cmd ks k} command tree is somewhat more complex and is made up of three parts:

\begin{itemize}
\item A command \icode{cmd} that may have a signature,
\item A list of subcontinuations \icode{ks},
\item An optional continuation (also called join-point) \icode{k}.
\end{itemize}

We will call the data type \icode{Tree} a semantic command tree and \icode{Tps} a syntactic command tree.

Semantic command trees are well suited for defining the initial translation into CPS. However, the abstract nature of semantic command trees prevents us from defining closure conversion without escaping from its abstraction. The fundamental problem is that continuations are defined as functions in the metalanguage instead of syntactic constructs in the IR itself. This prevents us from doing free variable analysis, which is at the heart of closure conversion.

Syntactic command trees have, as their name implies, syntactic representation of name binding in their constructors. They are specific to the notion of \icode{val}ues in \icode{Cps}. We require a function to translate from our semantic command trees to syntactic command trees in order to compile our lambda calculus source code. Because syntactic and semantic command trees represent the same piece of source code, we conjecture that a function that translates syntactic command trees back into semantic command trees must also exist. We have not implemented such a function yet, see section \ref{section:ctreebetter}.

Command trees are implemented using Haskell's Generalised Algebraic Data Type (GADT). A GADT gives us the power to add more types to our IR. The notation of a GADT is somewhat different than the notation of a normal data type. After the \icode{data} keyword we specify a name and type parameters, a \icode{where} keyword follows. Then the different constructors of the GADT are declared. The constructors are given a name followed by \icode{::}. The types of the arguments follow, separated by \icode{->}. Finally, we declare the constructor with its type parameters, which can relate to the types of the arguments.

Command trees are defined by a GADT as follows:

\begin{lstlisting}[language=Haskell]
-- semantic command tree
data Tree sig a where
  Leaf :: a -> Tree sig a
  Node :: sig n b p r q ->
          Vec n (p -> Tree sig r) ->
          Option b (q -> Tree sig a) ->
          Tree sig a
-- syntactic command tree
data Tps (sig :: Sig) a where
  Leaf :: a -> Tps sig a
  Node :: sig n b p r q ->
          Vec n (Tps sig Val) ->
          Option b (Var, Tps sig a) ->
          Tps sig a
\end{lstlisting}

A command tree does not know what set of commands is used. However, it does encode strong type constraints on its subcontinuations and continuation. To enforce these constraints we use a signature \icode{sig}. This way a command tells the command tree what comes after itself. More formally a signature has type:

\begin{lstlisting}[language=Haskell]
type Sig = Nat -> Bool -> * -> * -> * -> *
\end{lstlisting}

A signature instance \icode{sig n b p r q} tells us the following:

\begin{itemize}
\item The tag of the command and its enclosed parameters \icode{sig}.
\item The number of subcontinuations \icode{n}.
\item If the command has a continuation \icode{b}.
\item The argument \icode{p} and return type \icode{r} of the subcontinuations.
\item The argument type \icode{q} of the continuation.
\end{itemize}

Notice that \icode{Tps} does not use all signature information. \icode{Tps} is specialized to mirror the structure and types of \icode{Cps} and thus only uses the \icode{n} and \icode{b} part of the signature.

Without commands we can not do anything with our command trees. We will use a set of commands that reflect the constructors from the \icode{Cps} data type. We also define a number of extra commands that will help us compile.

\begin{lstlisting}[language=Haskell]
data Base :: Sig where
  Add    :: Val -> Val ->        Base Z     True  Void Void Val
  App    :: Val -> [Val] ->      Base Z     False Void Void Val

data Fix :: Sig where
  Fix    :: Vec n (Var,[Var]) -> Fix n      True  ()   Val  ()

data Comp :: Sig where
  GetK   :: Var ->               Comp Z     True  Val  Val  Val
  SetK   :: Var -> Val ->        Comp Z     True  Val  Val  ()
  Block  ::                      Comp (S Z) True  ()   Val  Val
  Fresh  :: Var ->               Comp Z     True  Void Void Var

data Record :: Sig where
  Record :: [Val] ->             Record Z   True  Void Void Val
  Select :: Int -> Val ->        Record Z   True  Void Void Val

data Malloc :: Sig where
  Malloc :: Int ->               Malloc Z   True  Void Void Val
  Load   :: Int -> Val ->        Malloc Z   True  Void Void Val
  Store  :: Int -> Val -> Val -> Malloc Z   True  Void Void ()

data Empty :: Sig where
\end{lstlisting}

If we look at the definition of commmands above, we can see that our original constructors \icode{Add}, \icode{App}, \icode{Fix} from the \icode{Cps} data type are here. The \icode{Val} constructor will be modeled by the \icode{Leaf} constructor. The \icode{Val} type is the same as in \icode{Cps}. The four compilation commands represent a continuation store (\icode{Getk,Setk}), command concatenation (\icode{Block}), and fresh variable name generation (\icode{Fresh}). We also have the \icode{Malloc} commands to represent the heap operations of \icode{Wat}.

We take a look at the signatures of the commands to see how they are structured. The \icode{Block} and \icode{Fix} command are the only ones that have subcontinuations. The \icode{Void} type indicates that there are no subcontinuations. \icode{Fix} has a subcontinuation for every function definition. This is required by the constructor itself as the natural number \icode{n} appears in both the function name with arguments and subcontinuations. The \icode{App} command does not have a continuation, which will lead to some trouble when trying to concatenate it with other commands. The return type of commands indicates that commands will bind to a variable \icode{Val}, or only produce a side-effect \icode{()}.

The \icode{Empty} command is special because it does not have any members. It is used to write transformations generically. For example, we define a transformation of a command to another command as the following function:

\begin{lstlisting}[language=Haskell]
foo :: Tps (cmd :+: rest) Val -> Tps (cmd' :+: rest) Val
\end{lstlisting}

This transformation should also work if the only command in the tree is \icode{cmd}. The tree would have type \icode{Tps cmd Val}, but this does not match because there is no \icode{rest}. This is where the \icode{Empty} command takes the place of \icode{rest}:

\begin{lstlisting}[language=Haskell]
foo :: Tps (cmd :+: Empty) Val -> Tps (cmd' :+: Empty) Val
\end{lstlisting}

The \icode{rest} problem is caused by the way we have implemented open unions in Haskell, see subsection \ref{subsection:openunion} and section \ref{section:ctreebetter}.

Let's see how our example \icode{(\ x -> x + 1) 41)} translates to a semantic command tree. The translation is very similar to the \icode{Cps} translation. We use a \icode{Fix} command to create a function \icode{f} with argument \icode{x}. The body of this function adds \icode{x} and the number one \icode{1}. The continuation is a leaf node, which serves the same purpose as the \icode{Val} constructor. The function name and arguments are separated by the function body, which is a vector of thunks (functions that take \icode{()} as argument). The continuation of our function node is an application node. Here we apply the function \icode{f} to the argument \icode{41}.
  
\begin{lstlisting}[language=Haskell]
Node (L (Fix (("f", ["x"]) ::: Nil))) ((\ () ->
  Node (R (Add (VAR "x") (INT 1)) Nil (Some (\ n -> Leaf n)))) ::: Nil) (Some (\ () ->
Node (R (App (VAR "f") [INT 41])) Nil None))
\end{lstlisting}

The syntactic syntax is similar to its semantic counterpart. The main difference is that function variables have been replaced with strings. In this case we use xthe empty string \icode{""} to represents continuations that take \icode{()} as an argument.

\begin{lstlisting}[language=Haskell]
Node (L (Fix (("f", ["x"]) ::: Nil))) ((
  Node (R (Add (VAR "x") (INT 1))) Nil (Some ("n", Leaf (VAR "n")))) ::: Nil) (Some ("",
Node (R (App (VAR "f") [INT 41])) Nil None))
\end{lstlisting}

\subsubsection{\label{subsection:openunion}Open Unions}
In order to compile \icode{Lam} into \icode{Wat} we will have to make use of all our command modules. We will combine our commands using an open union or extensible sum data type. An open union can be viewed as a list of data types. More precisely, it is a binary tree which has data types as leaf nodes. \icode{:+:} is right-associative and has two constructors \icode{L} and \icode{R}, which inject a data type into the left or right side of the tree, respectively \autocite{DBLP:conf/popl/LiangHJ95}. Note that we can nest instances of open unions to create open unions. Open unions make our command tree modular, because we can add new commands to an existing union to represent new language features.

\begin{lstlisting}[language=Haskell]
data (:+:) :: Sig -> Sig -> Sig where
  L :: sigl n b p r q -> (sigl :+: sigr) n b p r q
  R :: sigr n b p r q -> (sigl :+: sigr) n b p r q
\end{lstlisting}

\begin{lstlisting}[language=Haskell]
class (sub :: Sig) :<: (sup :: Sig) where
  inj :: sub n b p r q -> sup n b p r q
\end{lstlisting}

The typeclass \icode{:<:} allows us to inject data types into an open union automatically. We use smart constructors to mitegate the syntactic overhead of injecting \autocite{DBLP:conf/popl/LiangHJ95} even further. For example to lift our \icode{Add} command into the command tree we can define the following function. The constraint \icode{Base :<: cmd} ensures that \icode{Add} is located somewhere in the commands of the command tree. We will rarely use the original commands and mostly use their smart constructors when writing our tree transformations.

\begin{lstlisting}[language=Haskell]
add :: Base :<: cmd => Val -> Val -> Var -> Tps cmd ()
add v1 v2 x = liftT (inj (Add v1 v2)) Nil x (Leaf ())
\end{lstlisting}

\section{\label{section:treensforms}Tree Transformations}
Transformations for the new version of LamToWat will be implemented as Haskell functions. We will program mostly using \icode{do}-notation. A graph representation of the example lambda programs throughout the compilation process can be found in Appendix \ref{section:v2printsimple} and \ref{section:v2printnest}.

\subsection{\label{subsection:cpsconvert2}CPS Conversion}
Using \icode{Tree}, we can define an improved CPS conversion. The function is easier to read and write. We no longer have a metacontinuation hidden inside a continuation monad. This simplifies the notation significantly. We still operate in a monad, however, this monad is the command tree. The order of our listed operations matches the order of our final program more closely. There are some details that spoil the declarativity of our conversion somewhat. The advantages and disadvantages of our new approach become clear when we examine the conversion of a lambda abstraction in comparison to the one in the previous version of LamToWat.

\begin{lstlisting}[language=Haskell]
lam2tree :: Lam -> Tree (Comp :+: Fix :+: Base) Val
lam2tree (Val v) = return v
lam2tree (Lam x e) = do
  f <- fresh "f"
  k <- fresh "k"
  fix ((f,[x,k],do
           v <- lam2tree e
           app (VAR k) [v])
       ::: Nil)
  return (LABEL f)
lam2tree (App e1 e2) = block (do
  v1 <- lam2tree e1
  v2 <- lam2tree e2
  k <- getk "_nxt"
  app v1 [v2,k])
lam2tree (Add e1 e2) = do
  v1 <- lam2tree e1
  v2 <- lam2tree e2
  add v1 v2
\end{lstlisting}

We take a look at the conversion of \icode{Lam}. We generate a fresh function variable \icode{f} and continuation variable \icode{k}. We use these variables to create a function with name \icode{f} that has as argument the original variable and a continuation named \icode{k}. The body of the function will be the converted original body and a final statement that applies the continuation to the resulting variable. Finally, we return a \icode{LABEL} with the function name.

\subsection{\label{subsection:semtosyn}Tree Compilation}
The \icode{Tree} that is output by \icode{lam2tree} contains commands that represent effects. We will need to handle these commands and instantiate name binding commands with generated variables before we can closure convert. This is necessary in order to perform variable analysis. The transformation is called \icode{tree2tps} and is performed using effects for generating fresh variable names (\icode{fresh}) and accessing and updating an environment that associates \icode{Var}s with \icode{Val}s (\icode{ask},\icode{local}). The type of \icode{t2t} becomes:

\begin{lstlisting}[language=Haskell]
t2t :: Tree (Comp :+: Fix :+: Base) Val ->
       StateT Int (Reader [(Var,Val)]) Tps (Fix :+: Base :+: Empty) Val
\end{lstlisting}

Notice that we add the \icode{Empty} command to the end of the signature of \icode{Tps}. \icode{t2t} itself is defined as follows:

\begin{lstlisting}[language=Haskell]
t2t (Leaf x) = return (done x)
t2t (Node (R (R (Add v1 v2))) Nil (Some k)) = do
  x <- fresh "x"
  k' <- t2t (k (VAR x))
  return (add v1 v2 x k')
t2t (Node (R (R (App v vs))) Nil None) =
  return (app v vs)
t2t (Node (R (L (Fix fxs))) bs (Some k)) = do
  bs' <- mapM (\ b -> t2t (b ())) bsx
  k' <- t2t (k ())
  return (fix' fxs bs' k')
t2t (Node (L (SetK x v)) Nil (Some k)) =
  local ((x,v):) (t2t (k ()))
t2t (Node (L (GetK x)) Nil (Some k)) = do
  nv <- ask
  case lookup x nv of
    Just v -> t2t (k v)
    Nothing -> error (x ++ " is not in env " ++ show nv)
t2t (Node (L Block) (b ::: Nil) (Some k)) = do
  r <- fresh "r"
  x <- fresh "x"
  b' <- local
    (("_nxt",LABEL r):)
    (t2t (do v <- b ()
             T.app (LABEL r) [v]))
  k' <- t2t (k (VAR x))
  return (fix' ((r,[x]) ::: Nil) (k' ::: Nil) b')
t2t (Node (L (Fresh x)) Nil (Some k)) = do
  f <- fresh x
  t2t (k f)
\end{lstlisting}

The compilation of the \icode{Fresh} command seems trivial, because we use the helper function \icode{fresh}. This helper function should not be confused by the sugared version of the \icode{Fresh} command. This function updates the state and returns a fresh variable (in this case a string).

The compilation of \icode{Add} shows the instantiation of variables in the metalanguage with variables in the syntactic command trees. We generate a fresh variable \icode{x}, pass it to the continuation and compile the continuation, and finally plug the result into the syntactic command tree.

The \icode{SetK} and \icode{GetK} commands update and fetch named continuations. In our case there is only a continuation that is named \icode{_nxt}. The \icode{Block} command tells us to compile the continuation \icode{k} by passing it \icode{VAR x} and wrap it in a continuation function with name \icode{r} and argument {x}. We set the continuation \icode{_nxt} to the function label \icode{LABEL r}. We extend the body of the block with a final application of the continuation function \icode{r} to the result \icode{v} and compile with the updated continuation list. Finally, we create the continuation function and give it the compiled body \icode{b'} as continuation.

After tree compilation we obtain a \icode{Tps} with the commands \icode{Fix} and \icode{Base}. We have done two tranformations to obtain the same result as with \icode{Cps}. However, these transformations are significantly easier to write and the conversion from \icode{Tree} to \icode{Tps} only has to be written once if we keep the command set that \icode{Tree} has now.

\subsection{\label{subsection:closconvert2}Closure Conversion}
Now that we have eliminated the \icode{Comp} commands we can closure convert our syntactic command tree. We will follow the same approach as in the previous version of LamToWat: collect names of expression and use these to construct records. We will make the assumption that the only place where expressions are named (and thus our environment is extended) is in the join-point of a node and in functions. We do not hoist our function definitions to the top level immediately. We now have an extra transformation for this.

Separating the transformations also gives as a chance to better describe the type of the command tree before and after. The modular open unions of commands allows us to do so without having to write completely new data types which are mostly the same. Moreover, the transformations described in this section are modular: we can reuse them for different unions of commands.

The transformation only changes the \icode{Fix} and \icode{App} nodes. The other nodes simply extend the environment with their binding variable. The environment effect is now implemented as an extra argument to \icode{hCl}.

\begin{lstlisting}[language=Haskell]
hCl :: [Var] ->
       Tps            (Fix :+: Base :+: cmd) Val ->
       Tps (Record :+: Fix :+: Base :+: cmd) Val
hCl nv (Node (L (Fix fxs)) bs (Some (_,k))) =
  fix' (mapV addArg fxs)
       (zipWithV funClos fxs bs)
       (hCl nv k)
  where addArg (name,args) = (name,"_closure":args)

        funClos (name,args) body = do
          select_ 1 (VAR "_closure") "_env"
          zipWithM_ (openClos args) [0..] nv
          hCl (nv++args) body

        openClos args i x =
          if x `elem` args then return () else select_ i (VAR "_env") x

hCl nv (Node (R (L (App fun args))) Nil None) = do
  record_ (map VAR nv) "_env"
  args' <- mapM mkClos args

  case fun of
    LABEL fp -> let cl = '_' : fp in do
      record_ [LABEL fp,VAR "_env"] cl
      app (LABEL fp) (VAR cl : args')

    VAR cl -> let fp = '_' : cl in do
      select_ 0 (VAR cl) fp
      app (VAR fp) (VAR cl : args')

  where mkClos (LABEL x) = let _x = '_' : x in do
            record_ [LABEL x,VAR "_env"] _x
            return $ VAR _x
        mkClos v = return v

hCl nv (Leaf v) = Leaf v
hCl nv (Node cmd ks k) =
  Node (R cmd)
    (fmap (hCl nv) ks)
    (fmap (\ (x,k) -> (x,hCl (extendnv nv x) k)) k)
  where extendnv nv "" = nv
        extendnv nv x = nv ++ [x]
\end{lstlisting}

Before we hoist our function definitions to the top level we can transform our \icode{Record} commands into \icode{Malloc} commands. Here we see that we can fix our shortcoming of repeated constructors quite easily with command trees. We can focus on a particular command and translate it into its lower-level counterpart. In this case only \icode{Record} as truly translated as \icode{Select} and \icode{Load} have a one-to-one mapping.

\begin{lstlisting}[language=Haskell]
hRecord :: Tps (Record :+: cmd) Val -> Tps (Malloc :+: cmd) Val
hRecord (Node (L (Record vs)) Nil (Some (x,k))) = do
  malloc_ (length vs) x
  zipWithM_ (\ i -> store_ i (VAR x)) [0..] vs
  hRecord k

hRecord (Node (L (Select i v)) Nil (Some (x,k))) =
  load i v x (hRecord k)

hRecord (Leaf v) = Leaf v
hRecord (Node (R cmd) ks k) =
  Node (R cmd)
    (fmap hRecord ks)
    (fmap (fmap hRecord) k)
\end{lstlisting}

Hoisting is done with the \icode{hFix} function. We will need to be able to open the join-point of a \icode{Node} of our command tree in order to be able to hoist, because it is a non-local transformation. A non-local transformation affects the entire tree. In the case of hoisting we are chopping up the tree into individual commands, separating the \icode{Fix} commands and putting them into a list, and glueing the other commands back together to form a new tree. The type of \icode{hFix} clearly indicates that functions no longer contain other functions.

\begin{lstlisting}[language=Haskell]
hFix :: Tps (Fix :+: cmd) Val -> T.Fix (Tps cmd Val)
hFix (Leaf v) = ([],Leaf v)
hFix (Node (R cmd) ks k) = case k of

  Some (x,k) -> (fs++fs',Node cmd ks' (Some (x,k')))
    where (fs,k') = hFix k

  None -> (fs',Node cmd ks' None)

  where ks' = mapV (snd . hFix) ks
        fs' = concatMap (fst . hFix) $ toList ks

hFix (Node (L (Fix fxs)) bs (Some ("",k))) = (fs'++fs,k')
  where fs' = concat $ zipWith hFun (toList fxs) (toList bs)
        (fs,k') = hFix k

        hFun (f,as) b = (f,as,b') : fs
          where (fs,b') = hFix b
\end{lstlisting}

\subsection{\label{subsection:emit2}Emitting}
The emit step now becomes even more trivial, as we have also eliminated records from our command tree and replaced them with malloc commands. We include this step for completeness and testing purposes. We could use the command tree output by \icode{hFix} to generate WebAssembly code. The mapping is one-to-one for expressions: every remaining tree command has a \icode{Wat} counterpart. However, the transformation of \icode{Val}s does need to change labels into integers.

\begin{lstlisting}[language=Haskell]
tps2wat :: WatTps -> Wat
tps2wat (fs,e) = (map (fmap t2w) fs,t2w e)
  where ns = map (\ (f,as,b) -> f) fs

        t2w (Leaf v) = Val (v2v v)
        t2w (Node cmd ks k) = case (cmd,ks,k) of
          ((L (T.Malloc i)),      Nil, (Some (x,k))) -> Malloc i x (t2w k)
          ((L (T.Load i v)),      Nil, (Some (x,k))) -> Load i (v2v v) x (t2w k)
          ((L (T.Store i s t)),   Nil, (Some (_,k))) -> Store i (v2v s) (v2v t) (t2w k)
          ((R (L (T.Add v1 v2))), Nil, (Some (x,k))) -> Add (v2v v1) (v2v v2) x (t2w k)
          ((R (L (T.App v vs))),  Nil, None)         -> App (v2v v) (map v2v vs)

        v2v (LABEL x) = INT $ fromJust $ x `elemIndex` ns
        v2v v         = v
\end{lstlisting}

A \icode{ghci} session where we would run a converted lambda calculus expression would look as follows:

\begin{lstlisting}[language=Haskell]
> putStrLn $ emit $ tps2wat $ hFix $ swapTps $ hRecord $
    hClos $ tree2tps $ lam2tree $ parse "(\\ x -> x + 1) 41"
(module
...

> Wat.emitRun $ tps2wat $ hFix $ swapTps $ hRecord $
     hClos $ tree2tps $ lam2tree $ parse "(\\ x -> x + 1) 41"
_start() => i32:42
\end{lstlisting}

\section{\label{section:ctreebetter}Command Tree Improvements}
In this section we will explore the design space for command trees and discusss the shortcomings of the second version of LamToWat. During the development of the LamToWat compiler a number of different command trees were examined to see if they could provide us with a replacement for \icode{Cps}. We will discuss some of the relevant features here.

% blocks
Although command trees provide a useful abstraction for language implementers, it does require knowledge of the block model for control flow. Command trees help the programmer somewhat by providing metacontinuation store, which can be manipulated with the \icode{SetK} and \icode{GetK} commands. The language implementer will still need to wrap certain parts of code inside a block and fetch the right continuation at the right point. The responsibility is now put on the compiler writer, who has to compile the semantic command tree into a syntactic command tree.

% interpreter + morphism
In order to check intermediate results of the compiler after the initial CPS conversion, we would like to have an interpreter for \icode{Tree}. We can now only print \icode{Tree}, but we would like to map \icode{Tree} to a common domain. The abstract nature of \icode{Tree} is the main problem when writing an interpreter for it that is modular in the set of commands. A function called \icode{tps2tree} that transforms \icode{Tps} back into \icode{Tree} for the right set of commands would serve a similar purpose. This would show the isomorphism between the two and give another check of the compilation process.

% type constraints
Our command trees have some types, but we would like our types to do even more. For example we want a type to indicate that an expression is closed after closure conversion, i.e., it does not have any free variables. Transforming syntactic command trees into semantic ones would work as a sort of type checking function. Typing closure conversion has been studied in Haskell and other languages \autocite{DBLP:conf/haskell/GuillemetteM07, DBLP:conf/pldi/Chlipala07, DBLP:conf/popl/MorrisettWCG98}. We tried to implement something similar in LamToWat but found Haskell's type system uncooperative \autocite{10.1145/2578854.2503786}.

% better open unions
The implementation of open unions we have used for making \icode{Tps} modular can be improved. There is still some extra work required of the programmer. Extensible sums should behave like sets, but are implemented as binary trees. This means that the order of commands matters, e.g., \icode{A :+: B} is not the same as \icode{B :+: A} in the eyes of the Haskell type system. To mitigate this we can write helper functions that transform the structure of our extensible sums. There are a number of other implementations of open unions in the Haskell language which may provide the functionality we require \autocite{extensible-effects, open-union}. Haskell's typeclasses could be used to derive the necessary operations on open unions.

\section{\label{section:summarytree}Discussion}
In this chapter we have shown how the three shortcomings of our original compiler are eliminated by using command trees as IR. Each of the shortcomings addressed at the end of the previous chapter in section \ref{section:summarycps} is alleviated as follows: 

\begin{itemize}
\item CPS conversion becomes easier to specify by using blocks in \icode{lam2tree}
\item The type of the output of \icode{hFix} indicates that functions are no longer nested
\item \icode{hRecord} only transforms records and thus removes duplicate constructors
\end{itemize}

We separated \icode{Cps} into two command trees: semantic and syntactic. Semantic command trees give us the power to write a declarative CPS conversion function, improving the front-end of LamToWat. Syntactic command trees specific to \icode{Cps} allow the programmer to write modular, declarative transformations in the back-end of LamToWat without losing the ability to do variable analysis.

We can combine commands to create a modular approach to compilation. With the help of smart constructors and destructors we reduce syntactic overhead. More transformations are performed on the command tree than on \icode{Cps}, because we have to handle effects ourselves. Although it requires a little more effort on behalf of the programmer, it also provides a method to make transformations explicit and declarative. In the next chapter we will discuss the how we validated the performance of the compiler.
